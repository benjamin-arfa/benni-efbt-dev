/*******************************************************************************
 * Copyright (c) 2020 Bird Software Solutions Ltd
 * This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License 2.0
 * which accompanies this distribution, and is available at
 * https://www.eclipse.org/legal/epl-2.0/
 *
 * SPDE-License-Identifier: EPL-2.0
 *
 * Contributors:
 *    Neil Mackenzie - initial API and implementation
 *******************************************************************************/

grammar org.eclipse.efbt.ecore4reg.dsl.Ecore4Reg with org.eclipse.xtext.common.Terminals

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "http://www.eclipse.org/efbt/ecore4reg" as ecore4reg


Module returns ecore4reg::Module:
	RequirementsModule | TagGroup | GenerationRulesModule | ELPackage;
	
ELClassifier returns ecore4reg::ELClassifier:
	ELClass | ELDataType_Impl | ELEnum ;


ELStructuralFeature returns ecore4reg::ELStructuralFeature:
	ELAttribute | ELReference;


RequirementsSection returns ecore4reg::RequirementsSection:
	RequirementsSectionImage | RequirementsSectionLinkWithText | RequirementsSectionText | TitledRequirementsSection;


SelectColumn returns ecore4reg::SelectColumn:
	 SelectColumnMemberAs | SelectColumnAttributeAs | SelectDerivedColumnAs | SelectValueAs;



EString returns ecore::EString:
	STRING ;


RequirementsModule returns ecore4reg::RequirementsModule:
	{ecore4reg::RequirementsModule}
	'requirementsModule'
	name=QualifiedName
	(imports += Import)*
	'{'
		('annotation' '"dependency"' 'as' dependencies+=[ecore4reg::Module|QualifiedName])*
		('theRules' '{' rules+=TitledRequirementsSection ( "," rules+=TitledRequirementsSection)* '}' )?		
		('theAllowedtypes' allowedtypes=AllowedTypes)?
	'}';

GenerationRulesModule returns ecore4reg::GenerationRulesModule:
	{ecore4reg::GenerationRulesModule}
	'generationRuleModule'
	name=QualifiedName
	(imports += Import)*
	'{'
		('annotation' '"dependency"' 'as' dependencies+=[ecore4reg::Module|QualifiedName])*
		('generationRules' '{' rulesForReport+=RulesForReport ( "," rulesForReport+=RulesForReport)* '}' )?
	'}';



ELAttribute returns ecore4reg::ELAttribute:
	{ecore4reg::ELAttribute}
	(iD?='id')?
	eAttributeType=[ecore4reg::ELDataType|QualifiedName] ("[" ((  lowerBound=EInt) ".." ( upperBound=EInt))?   "]")?
	name=ID 
	;


ELClass returns ecore4reg::ELClass:
	{ecore4reg::ELClass}
	((eAbstract?='abstract'? 'class') ) name = ID
	('extends' eSuperTypes+=[ecore4reg::ELClass|QualifiedName] (',' eSuperTypes+=[ecore4reg::ELClass|QualifiedName])*)?
	'{'
	   (eStructuralFeatures+=ELStructuralFeature)*
	   (eOperations+=ELOperation)*
	'}'
	;

ELDataType_Impl returns ecore4reg::ELDataType:
	{ecore4reg::ELDataType}
	'type' name = ID
	'wraps' industryName = ID
	
	;

ELEnum returns ecore4reg::ELEnum:
	{ecore4reg::ELEnum}
	'enum' name = ID
	'{'
	  (eLiterals+=ELEnumLiteral ((',')? eLiterals+=ELEnumLiteral)*)?
	'}';

ELOperation returns ecore4reg::ELOperation:
	ELPublicOperation | ELPrivateOperation;
	
	
ELPublicOperation returns ecore4reg::ELPublicOperation:
	{ecore4reg::ELPublicOperation}
	'op'
	eType=[ecore4reg::ELClassifier|QualifiedName] ("["((  lowerBound=EInt) ".." ( upperBound=EInt))?  "]")?
	name=ID
	'()'
	'{'(body=EString)?'}'
	;
	
ELPrivateOperation returns ecore4reg::ELPrivateOperation:
	{ecore4reg::ELPrivateOperation}
	'private_op'
	eType=[ecore4reg::ELClassifier|QualifiedName] ("["((  lowerBound=EInt) ".." ( upperBound=EInt))?  "]")?
	name=ID
	'('(eParameters+=ELParameter (',' eParameters+=ELParameter)*)?')'
	'{'(body=EString)?'}'
	;


ELParameter returns ecore4reg::ELParameter:

    eType=[ecore4reg::ELClassifier|QualifiedName] ("["((  lowerBound=EInt) ".." ( upperBound=EInt))?  "]")?
    name=ID
;

ELReference returns ecore4reg::ELReference:
	{ecore4reg::ELReference}
	((containment?='contains' ) |
	 ('refers') 
	)
	eType=[ecore4reg::ELClassifier|QualifiedName] ("["((  lowerBound=EInt) ".." ( upperBound=EInt))?  "]")?
	name=ID
	;


ELEnumLiteral returns ecore4reg::ELEnumLiteral:
	{ecore4reg::ELEnumLiteral}
	name=ID
    ('as' literal=STRING)?
    ('=' value=EInt)?
    ;

EInt returns ecore::EInt:
	'-'? INT;


QualifiedName:
	ID ('.' ID)*;
	
Import returns ecore4reg::Import:
	{ecore4reg::Import}
        'import' importedNamespace=QualifiedNameWithWildcard;


QualifiedNameWithWildcard :
	QualifiedName '.*'?;
	



TitledRequirementsSection returns ecore4reg::TitledRequirementsSection:
	'titledRequirementsSection'
	name=ID
	'{'
		('theTitle' title=EString)?
		('theReferencingSections' referencingSections=[ecore4reg::RequirementsSectionLinkWithText|QualifiedName])?
		'theRequirementsType' requirementsType=[ecore4reg::RequirementType|QualifiedName]
		('theSections' '{' sections+=RequirementsSection ( "," sections+=RequirementsSection)* '}' )?
	'}';
	


AllowedTypes returns ecore4reg::AllowedTypes:
	{ecore4reg::AllowedTypes}
	'allAllowedTypes'
	'{'
		('theAllowedTypes' '{' allowedTypes+=RequirementType ( "," allowedTypes+=RequirementType)* '}' )?
	'}';





TagGroup returns ecore4reg::TagGroup:
	{ecore4reg::TagGroup}
	'tagGroup'
	name=QualifiedName
	(imports += Import)*
	'{'
		('annotation' '"dependency"' 'as' dependencies+=[ecore4reg::Module|QualifiedName])*
		('theTags' '{' tags+=Tag ( "," tags+=Tag)* '}' )?
	'}';
	
Tag returns ecore4reg::Tag:
	Tag_Impl | OperationTag;
	
OperationTag returns ecore4reg::OperationTag:
	{ecore4reg::OperationTag}
	'operationTag'
	name=ID
	'{'
		('theDisplayName' displayName=EString)?
		('theRequirements' '(' requirements+=[ecore4reg::TitledRequirementsSection|QualifiedName] ( "," requirements+=[ecore4reg::TitledRequirementsSection|QualifiedName])* ')' )?
		('theOperation' operation=[ecore4reg::ELOperation|QualifiedName])?
	'}';

Tag_Impl returns ecore4reg::Tag:
	{ecore4reg::Tag}
	'tag'
	name=ID
	'{'
		('theDisplayName' displayName=EString)?
		('theRequirements' '(' requirements+=[ecore4reg::TitledRequirementsSection|QualifiedName] ( "," requirements+=[ecore4reg::TitledRequirementsSection|QualifiedName])* ')' )?
	'}';



RequirementsSectionLinkWithText returns ecore4reg::RequirementsSectionLinkWithText:
	{ecore4reg::RequirementsSectionLinkWithText}
	'requirementsSectionLinkWithText'
	name=ID
	'{'
		('theLinkText' linkText=EString)?
		('theSubsection' subsection=EString)?
		('theLinkedRuleSection' linkedRuleSection=[ecore4reg::TitledRequirementsSection|QualifiedName])?
	'}';

RequirementType returns ecore4reg::RequirementType:
	{ecore4reg::RequirementType}
	'requirementType'
	name=ID;

RequirementsSectionImage returns ecore4reg::RequirementsSectionImage:
	{ecore4reg::RequirementsSectionImage}
	'requirementsSectionImage'
	name=ID
	'{'
		('theStyle' style=EString)?
		('theUri' uri=EString)?
	'}';

RequirementsSectionText returns ecore4reg::RequirementsSectionText:
	{ecore4reg::RequirementsSectionText}
	'requirementsSectionText'
	name=ID
	'{'
		('theText' text=EString)?
	'}';

ELPackage returns ecore4reg::ELPackage:
	{ecore4reg::ELPackage}
	'package'
	name = QualifiedName
	(imports += Import)*
	(eClassifiers += ELClassifier)*
	;

RulesForReport returns ecore4reg::RulesForReport:
	{ecore4reg::RulesForReport}
	'report' outputLayerCube=[ecore4reg::ELClass|QualifiedName]
	'{'
		(rulesForTable+=RulesForILTable (rulesForTable+=RulesForILTable)*  )?
	'}';
	
RulesForILTable returns ecore4reg::RulesForILTable:
	{ecore4reg::RulesForILTable}
	'ILTable' inputLayerTable=[ecore4reg::ELClass|QualifiedName]
	'{'
		(rulesForTablePart+=RuleForILTablePart (rulesForTablePart+=RuleForILTablePart)*  )?
	'}';
	
RuleForILTablePart returns ecore4reg::RuleForILTablePart:
	{ecore4reg::RuleForILTablePart}
	'tablePart'
	name=ID
	'{'
		( columns+=SelectColumn ( columns+=SelectColumn)*  )?
		('where' whereClause=TableFilter)?
	'}';



	
	
TableFilter returns ecore4reg::TableFilter:
	{ecore4reg::TableFilter}
		 (predicate=Predicate )?
	;

Predicate returns ecore4reg::Predicate:
	AndPredicate | OrPredicate | NotPredicate | AttributePredicate;
	
AndPredicate returns ecore4reg::AndPredicate:
	{ecore4reg::AndPredicate}
	'And('
		(operands+=Predicate ( "and" operands+=Predicate)*  )?
	')'
;

OrPredicate returns ecore4reg::OrPredicate:
	{ecore4reg::OrPredicate}
	'Or('
		(operands+=Predicate ( "or" operands+=Predicate)*  )?
	')'
;

NotPredicate returns ecore4reg::NotPredicate:
	{ecore4reg::NotPredicate}
	'Not('
		( operand=Predicate)?
	')';

AttributePredicate returns ecore4reg::AttributePredicate:
	{ecore4reg::AttributePredicate}
	'filter'

		( attribute1=[ecore4reg::ELAttribute|QualifiedName])?
		( comparitor=Comparitor)?
		('theMember' member=[ecore4reg::ELEnumLiteral|QualifiedName])?
		('theValue' value=EString)?
	;
	
SelectColumnMemberAs returns ecore4reg::SelectColumnMemberAs:
	{ecore4reg::SelectColumnMemberAs}
	'selectMember'
		( memberAsConstant=[ecore4reg::ELEnumLiteral|QualifiedName])?
		('as' asAttribute=[ecore4reg::ELPublicOperation|QualifiedName])?
		;
	
SelectColumnAttributeAs returns ecore4reg::SelectColumnAttributeAs:
	{ecore4reg::SelectColumnAttributeAs}
	'selectAttribute'
		( attribute=[ecore4reg::ELAttribute|QualifiedName])?
		('as' asAttribute=[ecore4reg::ELPublicOperation|QualifiedName])?
	;
	
SelectDerivedColumnAs returns ecore4reg::SelectDerivedColumnAs:
	{ecore4reg::SelectDerivedColumnAs}
	'selectDerivedAttribute'
		( attribute=[ecore4reg::ELPublicOperation|QualifiedName])?
		('as' asAttribute=[ecore4reg::ELPublicOperation|QualifiedName])?
	;
	
SelectValueAs returns ecore4reg::SelectValueAs:
	{ecore4reg::SelectValueAs}
	'selectValue'
		( value=EString)?
		('as' asAttribute=[ecore4reg::ELPublicOperation|QualifiedName])?
	;

enum Comparitor returns ecore4reg::Comparitor:
				less_than = 'is_less_than' | equals = 'is_equals' | not_equals = 'is_not_equals' | greater_than = 'is_greater_than';


	